#!/usr/bin/perl
our $VERSION = '0.020';
use strict;
use Getopt::Long;
use Pod::Usage;

sub SELF { 'dontbeahero' };

my $opt = { };
$opt->{delay}    = 590; # 9 minutes
$opt->{stdout}   = 0;
$opt->{lockfile} = '/tmp/dontbeahero.pid';

GetOptions(
  's|stdout'   => \$opt->{stdout},
  'd|delay'    => \$opt->{delay},
  'l|lockfile' => \$opt->{lockfile},
  'k|stop'     => \$opt->{kill},

  'h|help'     => sub { pod2usage(verbose => 1), exit },
  'v|version'  => sub { printf("%s v%s\n", SELF, $VERSION), exit },
  'm|man'      => sub { pod2usage(verbose => 3), exit },
);

reap() if $opt->{kill};
if(-f $opt->{lockfile}) {
  die("$opt->{lockfile} exists! Is " . SELF . " already running?\n");
}

dontbeahero();

sub dontbeahero {
  daemonize() unless $opt->{stdout};

  printf("%10s: %s\n", 'stdout',   $opt->{stdout});
  printf("%10s: %s\n", 'delay',    $opt->{delay});
  printf("%10s: %s\n", 'lockfile', $opt->{lockfile});

  my $y = 0;
  while(1) {
    $y ^= 1;
    mousemove(0, $y);
    sleep $opt->{delay};
  }
}

sub mousemove {
  my($z, $y) = @_ ? @_ : (1, 1);
  system('xdotool', 'mousemove', @_);
}

sub daemonize {
  require POSIX;
  my $pid_child = fork();
  exit 0 if $pid_child;
  exit 1 if ! defined $pid_child;

  POSIX::setsid();

  $pid_child = fork();
  exit 1 if ! defined $pid_child;

  if($pid_child) { # parent
    waitpid($pid_child, 0);
    unlink($opt->{lockfile});
    exit 0;
  }
  elsif($pid_child == 0) { # child
    open(my $fh, '>', $opt->{lockfile})
      or die("Can'aaaaat open $opt->{lockfile}: $!\n");
    print {$fh} $$;
    close $fh;

    open(STDOUT, '>', '/dev/null');
    open(STDERR, '>', '/dev/null');
    open(STDIN,  '<', '/dev/null');
  }
}

sub reap {
  open(my $fh, '<', $opt->{lockfile});
  chomp(my $target = <$fh>);
  if($target =~ m/^[0-9]+$/) {
    kill(9, $target) or die("Can't kill PID $target: $!\n");
  }
  else {
    die("dontbeahero isn't running: no one to reap\n");
  }
  exit;
}

#$SIG{TERM} = &reap;
#$SIG{KILL} = &reap;


__END__

=pod

=head1 NAME

dontbeahero - don't worry, someone else will save the planet

=head1 USAGE

dontbeahero [OPTION]...

=head1 OPTIONS

  -s,  --stdout    don't daemonize
  -d,  --delay     set delay
  -l,  --lockfile  specify lockfile
  -k,  --stop      stop the daemon

  -h,  --help      show the help and exit
  -v,  --version   show version info and exit
  -m,  --man       show the manual and exit

=head1 DESCRIPTION

I often play music videos on my TV when I code. When I need to take a break
I usually go and get some pineapple slices and make myself comfortable in
the sofa.

The builtin power saving features can't be turned off enough to make the screen
not darken itself, so I had to manually press a key on the tiny htpc keyboard to
get the picture back on. Accidently I often pressed a button that'd quit mplayer
or do other nasty things like jumping 50 videos back in the playlist.

The initial solution was to run

    xdotool mousemove 0 0

in a crontab. This worked great the first time. The second time around there's
nowhere to move and the screen stays black.

B<dontbeahero> uses a delay of 540 seconds (9 minutes) by default. It seems most 
monitors turn themselves off after 10 or 15 minutes.

=head1 PREREQUISITES

L<xdotool|http://www.semicomplete.com/projects/xdotool/>

=head1 AUTHOR

  Magnus Woldrich
  CPAN ID: WOLDRICH
  m@japh.se
  http://japh.se

=head1 CONTRIBUTORS

L<Dmitri Iouchtchenko|https://github.com/0>

=head1 LICENSE

This application is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut
